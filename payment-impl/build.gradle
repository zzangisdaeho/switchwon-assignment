plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.3.0' // Avro 플러그인
}

group = 'com.switchwon'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation(project(":payment"))
    implementation(project(":payment-share"))

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'
    // https://mvnrepository.com/artifact/org.apache.avro/avro
    implementation 'org.apache.avro:avro:1.11.3'
//    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-avro:2.13.3'

//    compileOnly 'org.projectlombok:lombok'
//    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

task checkoutSchema {
    doLast {
        File avroDir = file("${projectDir}/src/main/avro")
        if (avroDir.exists()) {
            exec {
                workingDir avroDir
                commandLine 'git', 'pull'
            }
        } else {
            exec {
                commandLine 'git', 'clone', 'https://github.com/zzangisdaeho/switchwon-assignment-avro-schema.git', avroDir
            }
        }
    }
}

// checkoutSchema 테스크가 실행된 후에 Avro 소스를 생성
tasks.named('generateAvroJava') {
    dependsOn checkoutSchema
}

// Avro 플러그인 설정
avro {
    createSetters = true // setter 메소드 생성 여부
    fieldVisibility = 'PRIVATE' // 필드 가시성 설정
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-main-avro-java'
        }
    }
}